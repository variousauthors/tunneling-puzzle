title Tunneling
author various authors
homepage www.puzzlescript.net
zoomscreen 18x9
run_rules_on_level_start

========
OBJECTS
========

Background 
black  

Floor
#A87600 #AB770D
01010
10011
00100
11001
01010

Target 
DarkBlue    

Wall 
DARKBROWN

Player (camera)
TRANSPARENT

Engineer
blue

Robot
red

Controls
orange

Controller
TRANSPARENT

Source
GREY blue
00000
00000
00100
00000
00000

US
grey blue
..1..
..1..
..1..
.....
.....

DS
LIGHTGRAY blue
.....
.....
..1..
..1..
..1..

LS
grey blue
.....
.....
111..
.....
.....

RS
grey blue
.....
.....
..111
.....
.....

overload
red
.....
.....
..0..
.....
.....

UIHorizontal
LIGHTGREEN
.....
.....
00000
.....
.....

UIVertical
LIGHTGREEN
..0..
..0..
..0..
..0..
..0..

UICornerTL
LIGHTGREEN
.....
.....
..000
..0..
..0..

UICornerTR
LIGHTGREEN
.....
.....
000..
..0..
..0..

UICornerBL
LIGHTGREEN
..0..
..0..
..000
.....
.....

UICornerBR
LIGHTGREEN
..0..
..0..
000..
.....
.....

HorizontalArm
DARKGRAY
00000
00000
00000
00000
00000

VerticalArm
DARKGRAY
00000
00000
00000
00000
00000

FirstTurn
TRANSPARENT

Shadow
TRANSPARENT

=======
LEGEND
=======

voidscape = Background

. = Floor
# = Wall and Floor
A = Robot and Floor
O = Target and Floor
S = Source and Floor
c = Controls and Floor

? = voidscape
' = voidscape

| = UIVertical and voidscape
- = UIHorizontal and voidscape
⌜ = UICornerTL and voidscape
⌝ = UICornerTR and voidscape
⌞ = UICornerBL and voidscape
⌟ = UICornerBR and voidscape

[ = VerticalArm and Floor
] = HorizontalArm and Floor

Arm = HorizontalArm or VerticalArm
Overlays = DS or US or LS or RS
Pushable = Source
Pusher = Player or Robot
Subject = Engineer or Robot

UI = UIHorizontal or UIVertical or UICornerTL or UICornerTR or UICornerBL or UICornerBR
Camera = Player
Start = Player and Engineer and Floor
P = Start and FirstTurn

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

voidscape,
Floor
Target, Controls
Wall, Source, UI, Player, Arm
Engineer, Robot
US, Controller
DS
LS
RS, FirstTurn
overload, Shadow

======
RULES     
======     

( NOTES )
( something should happen if the player makes a loop )
( I was thinking everything inside the loop is obliterated )
( and replaced with traversable voidscape )
( and a shocking sound effect plays )
( but it should be impossible to make a loop for the first little while )

( we will hide some sources outside under the snow )
( this will work better if the sources have some depth, )
( but depth means the source dot can't be centered... )

( on every turn after the first )
( we want the turn to cancel if the player did not move )
( this is because the player controls decorators like the Engineer )
( which do not interact with blocks on their own )

[Player no FirstTurn] -> [Player Shadow]
[FirstTurn] -> []
late [Player Shadow] -> cancel
late [Shadow] -> []

( the player pilotes the engineer )
[> Player Engineer] -> [> Player > Engineer]

( the Engineer pilotes the arms )
horizontal [> Player Robot][HorizontalArm] -> [> Player Robot][> HorizontalArm]
vertical [> Player Robot][VerticalArm] -> [> Player Robot][> VerticalArm]

( the arms control the robo )
[> Arm][Robot] -> [> Arm][> Robot]

( the engineer can activate and deactivate the robot )
[action Player Engineer Controls] [Robot] -> [Engineer Controls] [Player Robot]
[action Player Robot] [Engineer] -> [Robot] [Player Engineer]

( the engineer pilotes the robot )


( pushers push pushable things )
[ > Pusher | Pushable ] -> [  >  Pusher | > Pushable  ]

( clear the overlays )
[ Overlays ] -> []
[ overload ] -> []

( render overlays )

( adjacent sources transfer power )
late right [ Source | Source ] -> [ Source RS | Source LS ]
late down [ Source | Source ] -> [ Source DS | Source US ]

( power transfers through walls )
late right [ Source | Wall | Source ] -> [ Source RS | Wall RS LS | Source LS ]
late down [ Source | Wall | Source ] -> [ Source DS | Wall DS US | Source US ]

( overloads )
late [ RS LS DS ] -> [ RS LS DS overload ]
late [ RS LS US ] -> [ RS LS US overload ]
late [ US DS LS ] -> [ US DS LS overload ]
late [ US DS RS ] -> [ US DS RS overload ]


==============
WINCONDITIONS
==============


=======     
LEVELS
=======

##############????⌜--------⌝
#............#????|''''''''|
#.##########.#????|''''''''|
#.#.S.S....#.#????|''''''''|
#[#.A...O..#.#????⌞--------⌟
#.#...S.SS.#.#??????????????
#.##########.#??????????????
#...]...C....#??????????????
#............#??????????????
#..S..P..S...#??????????????
#...S....S...#??????????????
#............#??????????????
##############??????????????
